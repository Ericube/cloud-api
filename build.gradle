plugins {
    id 'java'
    id 'maven-publish'
}

def baseVersion = "0.0.7"

allprojects {
    group = "app.simplecloud.api"
    version = baseVersion

    repositories {
        mavenCentral()
        mavenLocal()
        maven { url "https://s01.oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://repo.papermc.io/repository/maven-public" }
        maven { url "https://repo.simplecloud.app/snapshots" }
        maven { url "https://buf.build/gen/maven" }
        maven { url "https://plugins.gradle.org/m2/" }
    }
}

subprojects {
    if (project.path.startsWith(":platform:")) {
        group = "app.simplecloud.api.platform"
    }

    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'com.github.johnrengelman.shadow'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(8)
        }
        withSourcesJar()
        withJavadocJar()
    }

    kotlin {
        jvmToolchain(8)
        compilerOptions {
            apiVersion = org.jetbrains.kotlin.gradle.dsl.KotlinVersion.KOTLIN_1_8
            jvmTarget = org.jetbrains.kotlin.gradle.dsl.JvmTarget.JVM_1_8
        }
    }

    publishing {
        repositories {
            // Repository SimpleCloud existant
            maven {
                name = "simplecloud"
                url = uri("https://repo.simplecloud.app/snapshots/")
                credentials {
                    username = System.getenv("SIMPLECLOUD_USERNAME") ?: project.findProperty("simplecloudUsername")
                    password = System.getenv("SIMPLECLOUD_PASSWORD") ?: project.findProperty("simplecloudPassword")
                }
                authentication {
                    basic(BasicAuthentication)
                }
            }

            // GitHub Packages
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/Ericube/cloud-api")
                credentials {
                    username = System.getenv("GITHUB_ACTOR") ?: System.getenv("ERI_USER")
                    password = System.getenv("GITHUB_TOKEN") ?: System.getenv("ERI_TOKEN")
                }
            }
        }

        publications {
            mavenJava(MavenPublication) {
                from components.java

                pom {
                    name = 'SimpleCloud API'
                    description = 'API for SimpleCloud'
                    url = 'https://github.com/Ericube/cloud-api'

                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }

                    developers {
                        developer {
                            id = 'ericube'
                            name = 'Ericube'
                        }
                    }

                    scm {
                        url = 'https://github.com/Ericube/cloud-api'
                        connection = 'scm:git:git://github.com/Ericube/cloud-api.git'
                        developerConnection = 'scm:git:ssh://github.com/Ericube/cloud-api.git'
                    }
                }
            }
        }
    }

    signing {
        sign publishing.publications
        useGpgCmd()
    }
}